#!/usr/bin/env perl

# modified gffseq.pl (Felix)

use warnings;
use strict;

use Getopt::Long;
use Pod::Usage;

use Bio::Tools::GFF;
use Bio::FeatureIO::gff;
use Bio::Tools::CodonTable;
use Bio::SeqIO;

=head1 OPTIONS

=over

=item --gff

Input gff file

=item [--version] [3]

Gff version.

=item --type

Required. Feature type, e.g. CDS, match_part ...

=item --source

Required. Feature annotation source, e.g. maker, snap_masked ...

=item --aa

Output protein translation of sequence.

=back

=head1 AUTHOR

Thomas Hackl S<thomas.hackl@uni-wuerzburg.de>

=cut

my ($gff,$version,$type,$source,$aa,$help,$man) = ("",3,"","",0,0,0);

GetOptions(
           'gff=s'=>\$gff,
           'version=i'=>\$version,
           'type=s'=>\$type ,
           'source=s'=>\$source,
           'aa!'=>\$aa,
           'help|?'=>\$help ,
           'man'=>\$man
          ) or pod2usage(2);

pod2usage(1) if $help;
pod2usage(-exitstatus => 0, -verbose => 2) if $man;

@ARGV && !$gff && ($gff=shift @ARGV);

# required
$gff || pod2usage("required: --gff");
$type || pod2usage("required: --type");
$source || pod2usage("required: --source");

# Initialise input stream
my $gffio = Bio::Tools::GFF->new(-file => $gff, -gff_version => $version) or die $!;

# Initialise codon table
my $codontable = Bio::Tools::CodonTable->new();

# Initialise sequence storage
my %sequences = ();

# Initialise storage for features
my %transcripts = ();

# Loop over the input stream
while(my $feature = $gffio->next_feature())
{ 

    my $primary_tag =  $feature->primary_tag();
    my $source_tag =  $feature->source_tag();
	
    # Apollo is not placing a source field into the gffs
    # Therefore also a dot as source is needed
    next unless ( $primary_tag eq $type && ( $source_tag eq $source || $source_tag eq "." ) );

    my $id;
    if ($feature->has_tag('Parent')) {
        ($id) = $feature->get_tag_values('Parent');  
    }elsif ($feature->has_tag('locus_tag')) {
        ($id) = $feature->get_tag_values('locus_tag');  
    }else {
        die "couldn't find Parent ID or locus tag as ID source\n";
    }
    push(@{$transcripts{$id}}, $feature);   
}

# Close input stream
$gffio->close() or die $!;

# Fill sequence storage
%sequences = map { ($_->display_id , $_->seq()) } $gffio->get_seqs();

# Start looping to transcripts
foreach my $id (keys %transcripts){

    # Use first CDS element to get strand of transcript
    my $strand = $transcripts{$id}[0]->strand();

    # Use first CDS element to get contig name
     my $contig = $transcripts{$id}[0]->seq_id();

    # Report Progress
    print STDERR "Working on transcript $id (contig $contig, strand $strand)\n";

    # Sort CDS elements after their starting position
    my $cds_sorted = sort_cds(\@{$transcripts{$id}},$strand);

    # Get Coding sequence
    my $cds_seq = get_cds_seq($cds_sorted, \%sequences, $strand);   

    # Get Gene Locus
    my ($cds_start,$cds_end) = get_loci($cds_sorted);

    # Get Coding sequence length
    my $cds_length = length($cds_seq);

    # Return result as user specified
    unless($aa){
	print ">$id CNT:$contig LOC:$cds_start-$cds_end LEN_CDS:$cds_length SRC:$source\n$cds_seq\n";
    }else{

        # Get Protein sequence
        my $prot_seq = $codontable->translate($cds_seq);
        
        # Get Protein sequence length
        my $prot_length = length($prot_seq);
        
        print ">$id CNT:$contig LOC:$cds_start-$cds_end LEN_CDS:$cds_length SRC:$source\n$prot_seq\n";
    }

}

#-------------------------------------------------------------------------------
#------------------------------- FUNCTIONS -------------------------------------
#-------------------------------------------------------------------------------

#-------------------------------------------------------------------------------

sub get_loci {

    my $cds_sorted = shift;

    my $start = $cds_sorted->[0]->start();
    my $end = $cds_sorted->[-1]->end();

    return($start,$end);

}

#-------------------------------------------------------------------------------

sub sort_cds {

    my $transcripts = shift;
    my $strand = shift;

    my @sorted = ();

    if($strand ==  1)
    {
                @sorted = sort { $a->start() <=> $b->start() } @{$transcripts};
    }
    elsif($strand == -1)
    {
                @sorted = sort { $b->start() <=> $a->start() } @{$transcripts};
    }
    else
    {
	die "Unknown strand in CDS!\n";
    }

    return \@sorted;

}

#-------------------------------------------------------------------------------

sub get_cds_seq {

    my $cds_sorted   = shift;
    my $sequences = shift;
    my $strand = shift;
    
    # Initialise CDS storage
    my $cds_seq = "";

    # Loop to CDS of each transcripts
    foreach my $cds (@{$cds_sorted})
    {
	
	# Get Start of CDS
	my $start = $cds->start();
	
	# Get End of CDS
	my $end = $cds->end();
	
	# Get Length of CDS
	my $length = abs($start-$end)+1;
	
	# Get parent sequence of CDS
	my $seq_id = $cds->seq_id();
	
	# Check for reverse strand
	if($strand == -1)
	{
	    
	    # Extract coding sequence
	    my $temp = substr($sequences->{$seq_id},$start-1,$length);
	    
	    # Get complement seuqence
	    $temp =~ tr/atugcyrswkmbdhvnATUGCYRSWKMBDHVN/taacgryswmkvhdbnTAACGRYSWMKVHDBN/;
	    
	    # Reverse sequence
	    $cds_seq .= reverse($temp);

	}

	# Check for forward strand
	elsif($strand == 1)
	{
	    # Extract coding sequence
	    $cds_seq .= substr($sequences->{$seq_id},$start-1,$length);
	}

    }

    # Return coding sequence
    return $cds_seq;
}

#-------------------------------------------------------------------------------
