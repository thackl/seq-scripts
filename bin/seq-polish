#!/usr/bin/env perl
# Created: 13 Oct 2021
# Author: Thomas Hackl, thackl@lim4.de
use warnings;
use strict;
use Getopt::Long  qw(:config no_ignore_case);
use File::Temp qw(tempdir);
use File::Basename qw(basename);
use Cwd 'abs_path';
use Data::Dumper;
$Data::Dumper::Sortkeys = 1;

my @il_reads;
my @pb_reads;
my @np_reads;

my %reads = (il => [], pb => [], np => []);

GetOptions (
    "out|o=s" => \(my $out),
    "dir|d=s" => \(my $dir = tempdir( CLEANUP => 1 )),
    "illumina|i=s{1,}" => $reads{il},
    "pacbio|p=s{1,}" => $reads{pb},
    "nanopore|n=s{1,}" => $reads{np},
    "threads|t=i" => \(my $threads = 1),
    "methods|m=s" => \(my $methods),
    "min-length|l=s" => \(my $min_length = "2000"),
    "medaka-model=s" => \(my $medaka_model="r941_min_high_g360"),
    "help|h!" => \(my $help),
    "debug|D!" => \(my $debug),
    "dryrun|y!" => \(my $dry),
) or die("Error in command line arguments\n");

if ($help || @ARGV != 1){
    print "NOTE: Because the read options accept multiple filenames,
  the assembly file has to be specified before the reads!\n\n" unless @ARGV;
    print "Usage: seq-polish [options] assembly.fna -i il.fq [il2.fq ...] -n np.fq [np2.fq ...] -p pb.fq [pb2.fq ...]\n";
    printf " %-19s  %s\n", "-i/--illumina", "Illumina reads";
    printf " %-19s  %s\n", "-n/--nanopore", "Nanopore reads";
    printf " %-19s  %s\n", "-p/--pacbio", "Pacbio reads";
    printf " %-19s  %s\n", "-l/--min-length", "minimum read length [$min_length]";
    printf " %-19s  %s\n", "-m/--methods", "which tools to run and how many iterations. Defaults:";
    printf " %-19s  %s\n", "", "- il-only: 'rr' - 2x racon";
    printf " %-19s  %s\n", "", "- np-only: 'rrm' - 2x racon + 1x medaka";
    printf " %-19s  %s\n", "", "- pb-only: 'rr' - 2x racon";
    printf " %-19s  %s\n", "", "- np+il: 'rrm,rr' - 2x racon + 1x medaka with Nanopore, 2x racon with Illumina";
    printf " %-19s  %s\n", "", "- pb+il: 'rr,rr' - 2x racon with Pacbio, 2x racon with Illumina";
    printf " %-19s  %s\n", "--medaka-model", "Modell for Medaka [$medaka_model]";
    printf " %-19s  %s\n", "-o/--out", "write to this file [STDOUT]";
    printf " %-19s  %s\n", "-t/--threads", "number of CPU threads";
    printf " %-19s  %s\n", "-h/--help", "show this help";
    printf " %-19s  %s\n", "-D/--debug", "show debug messages";
    exit 0;
}

my $in = $ARGV[0];

my %def_reads = (
    np => "Nanopore",
    il => "Illumina",
    pb => "PacBio"
    );

my %def_blocks  = (
    il => "rr",
    np => "rrm",
    pb => "rr",
    npil => "rrm,rr",
    pbil => "rr,rr"
);

my %def_methods = (
    "r" => "racon",
    "m" => "medaka",
);

my %def_calls = (
    "racon" => \&run_racon,
    "medaka" => \&run_medaka,
);

my @read_types;
for (qw(np pb il)){
    if (@{$reads{$_}}) {
        push @read_types, $_;
        print "$def_reads{$_} reads\n";
        print "  ", join("\n  ", @{$reads{$_}}), "\n\n";
    }
}

if (!$methods) {
    die "no default polishing sequence defined for the given read type combination: @read_types"
        if not exists $def_blocks{join("", @read_types)};
    $methods = $def_blocks{join("", @read_types)}
}
    
my @blocks = split(",", $methods);
if (@blocks != @read_types) {
    die "number of read types and polishing blocks differ:\n",
        join(", ", @blocks),"\n", join(", ", @read_types), "\n";
}


# create working dir
if (!-e $dir){mkdir $dir}
# copy and length filter infile this solves:
# - racon only knows fa/fasta ext
# - weird fasta format issues
# - (hopefully) possible medaka threading issues with small contigs
run("seqkit seq -m $min_length $in > $dir/i0.fa");
if (!defined $out) {
    $out = basename($in, qw(.fa .fas .fna .fasta))."-polished.fna";
}


my @runs;
my $i=0;
for (my $j=0; $j<@blocks; $j++) {
    my $block = $read_types[$j];
    my @iters = split("", $blocks[$j]);
    for (my $k=0; $k<@iters; $k++) {
        $i++;
        my $method = $iters[$k];

        die "Unknown method abbrevation '$method'\nKnown methods are:", Dumper(\%def_methods) unless
            exists($def_methods{$method});

        my $pre="$i-$block-$def_methods{$method}";
        print $pre, "\n" if $debug;

        push @runs, {
            i => $i,
            block => $block,
            reads => $reads{$block},
            method => $def_methods{$method},
            in => "$dir/i".($i-1).".fa",
            out => "$dir/i$i"
        };
    }
}

print Dumper(\@runs) if $debug;


# merge reads in single lib
print "Temporarily merging reads where necessary\n";
for (qw(np pb il)){
    if (@{$reads{$_}} > 1) {
        run("cat @{$reads{$_}} > $dir/il.fq");
        @{$reads{$_}} = "$dir/il.fq";
    }
}
print "\n";

for (@runs) {
    $def_calls{$_->{method}}(%$_);
}

# mv final output to top
run("seqkit seq $runs[$#runs]{out}.fa >$out"); # set final out to true out


sub run_racon{
    my %r = @_;
    print "Running racon\n";
    my %modes = (il => "sr", pb => "map-pb", np => "map-ont");
    my $mode = $modes{$r{block}};
    run("minimap2 -t $threads -x $mode $r{in} @{$r{reads}} > $r{out}.paf");
    run("racon -u -t $threads @{$r{reads}} $r{out}.paf $r{in} > $r{out}.fa");
    print "\n";
        

}

sub run_medaka{
    my %r = @_;
    print "Running medaka\n";

    my $cmd;
    my $pre = $r{out};

    my $xargs_threads = int(($threads+1)/2);

    # DEPRECATED: independent medaka chunks
    #run("SeqChunker -s $chunk_size $r{in} -o $dir/i$r{i}-c%04d.fa");
    #run("find $dir -name \"i$r{i}-c*.fa\" | xargs -t -n1 -i -P $threads medaka_consensus -t 1 -i @{$r{reads}} -o $mdir -d {} 2>> $mdir.log");
    #run("mv $mdir/consensus.fasta $pre");

    # first align reads to assembly
    print "# mapping\n";
    run("mini_align -i @{$r{reads}} -r $r{in} -m -p $pre -t $threads");

    # generate bed files for contig chunks
    print "# consensus\n";
    run("seq-len $r{in} | sed 's/\t.*\t/\t0\t/' | split -l 100 -a6 -d --additional-suffix .bed - $pre-");

    run("ls -1 $pre-*.bed |
       xargs -t -n1 -i -P $xargs_threads bash -c 'medaka consensus $pre.bam {}.hdf --model $medaka_model --batch 200 --threads 2 --region {} 2>> $pre.log'");

    print "# stitch\n";
    run("medaka stitch $pre-*.hdf $r{in} $pre.fa");

    print "\n";
}


sub run{
    my ($cmd) = @_;
    print $cmd, "\n";
    my ($prog) = $cmd =~ /(^\S+)/; 
    $dry || system($cmd) && die "$prog returned with an error!";
}
