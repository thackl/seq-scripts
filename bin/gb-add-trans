#!/usr/bin/env perl
use strict;
use Bio::SeqIO;
use Bio::Tools::CodonTable;
use Data::Dumper;

my(@Options, $verbose, $format, $hypo, $idtag, $sep, $desctags, $blank, $pseudo, $minlen, $gcode, $productlen);
setOptions();

my $in = Bio::SeqIO->new(-fh=>\*ARGV, -format=>$format);
my $out = Bio::SeqIO->new(-fh=>\*STDOUT, -format=>$format);

while (my $seq = $in->next_seq) {
  print STDERR "\rParsing: ",$seq->display_id, "\n";
  my $counter = 0;
  for my $f ($seq->get_SeqFeatures)   {
    
    if ($f->primary_tag eq 'gene') {
          $f->remove_tag('product');
    }
    next unless $f->primary_tag eq 'CDS';
        
    next unless $f->length >= $minlen;
    
    next if !$pseudo and $f->has_tag('pseudo');

    my $prod = TAG($f, 'product') || $blank;

    next if !$hypo and $prod eq 'hypothetical protein';

    my $cds = $f->spliced_seq;  # don't forget eukaryotes!

    $counter++;
    
    # HANDLE CODON START FOR FUZZY FEATURES!
    if ($f->has_tag('codon_start')) {
      my($cs) = $f->get_tag_values('codon_start');
      if ($cs != 1) {
        print STDERR "\t/codon_start=$cs - trimming mRNA!\n";
        $cds = $cds->trunc($cs, $cds->length);
      }
    }
    #END

    # DNA -> AA  
    my $tt = $gcode || TAG($f, 'transl_table') || 11;
    print STDERR "\tUsing specified /transl_table=$tt\n" if $verbose && $tt != 1;
    
    # http://www.bioperl.org/wiki/HOWTO:Beginners#Translating
    $cds = $cds->translate(-codontable_id => $tt, -complete => 1) if $tt >= 0;

    $f->add_tag_value('translation', $cds->seq);

    if($productlen && $prod && length($prod) > $productlen){
        $f->remove_tag('product');
        $f->add_tag_value('product', substr($prod, 0, $productlen));
    }
  }
  $out->write_seq($seq);
}
print STDERR "Done.\n";

#----------------------------------------------------------------------

sub TAG {
  my($f, $tag) = @_;
  return unless $f->has_tag($tag);
  # Seems new submissions have 2 protein IDs - one useful and one annoying!
  # /protein_id="REF_PRJNA193299:EFAU085_p1001"
  # /protein_id="YP_008390691.1"
  return ( grep { ! m/PRJNA/ } $f->get_tag_values($tag) ) [0];
}

#----------------------------------------------------------------------
# Option setting routines

sub setOptions {
  use Getopt::Long;

  @Options = (
    {OPT=>"help",      VAR=>\&usage,                       DESC=>"This help"},
    {OPT=>"verbose!",  VAR=>\$verbose, DEFAULT=>0,         DESC=>"Verbose progress"},
    {OPT=>"format=s",  VAR=>\$format,  DEFAULT=>'genbank', DESC=>"Input format"},
    {OPT=>"pseudo!",   VAR=>\$pseudo,  DEFAULT=>1,     DESC=>"Include /pseudo gene, --no-pseudo to disable"},
    {OPT=>"hypo!",     VAR=>\$hypo,    DEFAULT=>1,     DESC=>"Include 'hypothetical protein' genes, --no-hypo to disable"},
    {OPT=>"gcode=i",   VAR=>\$gcode,   DEFAULT=>0,     DESC=>"Force this genetic code for translation (otherwise /transl_table), -1 to disable translation"},
    {OPT=>"productlen=i",  VAR=>\$productlen,  DEFAULT=>'0', DESC=>"Maximum length of product description field, 0 to disable"},
  );

  (!@ARGV) && (usage());

  &GetOptions(map {$_->{OPT}, $_->{VAR}} @Options) || usage();

  # Now setup default values.
  foreach (@Options) {
    if (defined($_->{DEFAULT}) && !defined(${$_->{VAR}})) {
      ${$_->{VAR}} = $_->{DEFAULT};
    }
  }
}

sub usage {
  print "Usage: $0 [options] [genome1.gbk ...] > proteins.faa\n";
  foreach (@Options) {
    printf "  --%-13s %s%s.\n",$_->{OPT},$_->{DESC},
           defined($_->{DEFAULT}) ? " (default '$_->{DEFAULT}')" : "";
  }
  exit(1);
}
 
#----------------------------------------------------------------------
